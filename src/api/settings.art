
gets: $[container index][
    switch throws? [discard get container index]
        -> null
        -> get container index
]

filterKeys: $[container :dictionary allowed :block][
    result: #[]

    loop (to [:literal] allowed) 'key [
        if key? container key [
            set result key (gets container key)
        ]
    ]

    result
]

define :Settings [
    init: constructor [
        path :string
        suffix :string
        binary :string
    ]

    dictionary: method [][
        #[
            path: this\path
            suffix: this\suffix
            binary: this\binary
        ]
    ]
]


define :FileSettings is :Settings [
    init: method [file :string][
        data: this\load file

        this\path: gets data 'path
        this\suffix: gets data 'suffix
        this\binary: gets data 'binary
        this\data: data
    ]

    load: method [file :string][
        when [
            suffix? file ".art" -> # file
            suffix? file ".toml" -> read.toml file 
            not? exists? file -> #[]
        ]
    ]

    dictionary: method [][
        filterKeys this\data [path suffix binary]
    ]
]

define :CliSettings is :Settings [
    init: method [data :dictionary][
        this\path: gets data 'path
        this\suffix: gets data 'suffix
        this\binary: gets data 'binary
        this\data: data
    ]

    dictionary: method [][
        filterKeys this\data [path suffix binary]
    ]
]

mergeSettings: $[others :block][
    ensure -> every? others 'o [is? :Settings o]
    settings: #[
        path: "specs"
        suffix: ".spec.art"
        binary: "arturo"
    ]

    loop others 's [
        loop (to :dictionary s) [key val][
            set settings key val
        ]
    ]

    to :Settings settings
]

if standalone? ::

    ; Test CLI settings

    cli: to :CliSettings @[#[
        path: "tests"
        suffix: ".test.art"
        otherStuff: "ignored"
    ]]

    ensure -> cli\path = "tests"
    ensure -> cli\suffix = ".test.art"
    ensure -> cli\binary = null
    ensure -> (to :dictionary cli) = #[path: "tests" suffix: ".test.art"]

    ; Test Arturo file settings

    {
        path: "tests"
        suffix: ".test.art"
    } >> "settings.art"

    file: to :FileSettings ["settings.art"]

    ensure -> file\path = "tests"
    ensure -> file\suffix = ".test.art"
    ensure -> file\binary = null
    ensure -> (to :dictionary file) = #[path: "tests" suffix: ".test.art"]

    delete "settings.art"

    ; Test TOML file settings

    {
        path = "tests"
        suffix = ".test.art"
    } >> "settings.toml"

    file: to :FileSettings ["settings.toml"]

    ensure -> file\path = "tests"
    ensure -> file\suffix = ".test.art"
    ensure -> file\binary = null
    ensure -> (to :dictionary file) = #[path: "tests" suffix: ".test.art"]

    delete "settings.toml"

    ; Test merging settings

    {
        path: "tests"
        binary: "custom-arturo"
    } >> "settings.art"

    file: to :FileSettings ["settings.art"]

    cli: to :CliSettings @[#[
        suffix: ".test.art"
        path: "e2e"
    ]]

    final: mergeSettings @[file cli]

    ensure -> final\path = "e2e"
    ensure -> final\suffix = ".test.art"
    ensure -> final\binary = "custom-arturo"
    ensure -> (to :dictionary final) = #[path: "e2e" suffix: ".test.art" binary: "custom-arturo"]

    delete "settings.art"
