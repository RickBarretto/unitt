define :AssertionResult [
    init: constructor [assertion :block result :boolean]
]

define :TestResult [
    init: constructor [description :string]
]

define :StandaloneResult [
    init: constructor [tests :block]

    string: method [][
        if empty? this\tests [
            return ""
        ]

        join.lines flatten @[
            ~"Standalone Tests:"
            to [:string] this\tests
        ]
    ]
]

define :SpecResult [
    init: constructor [description :string tests :block]

    string: method [][
        join.lines flatten @[
            ~"Describe: |this\description|"
            ""
            to [:string] this\tests
        ]
    ]
]

define :FileResult [
    init: constructor [filename :string standalone :block specs :block]

    string: method [][
        standalone: to :string to :StandaloneResult [this\standalone]
        specs: to :string to [:SpecResult] this\specs

        join.lines @[
            ~"======== |this\filename| ========"
            ""
            standalone
            ""
            specs
            ""
        ]
    ]

]

define :Result [
    init: constructor [files :block]

    block: method [][
        this\files 
            | map => [read.json &]
            | map => [to :FileResult &]
    ]

    string: method [][
        to :block this | join.lines
    ]
]

findResults: $[at :string][
    files: at 
        | list.recursive 
        | map => normalize

    to :Result [files]
]

