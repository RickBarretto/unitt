as: $[value :any target :type :block][
    to target value
]

define :AssertionResult [
    init: constructor [expression :string passed? :logical]

    string: method [][
        status: (this\passed?)?
            -> "✅"
            -> "❌"

        @[status this\expression]
            | join.words
    ]

]

define :TestResult [
    init: constructor [description :string assertions :block]

    string: method [][
        description: @["Description:" this\description]
            | join.words

        assertions: this\assertions 
            | as [:AssertionResult]
            | as [:string]
            | map => indent

        @[description assertions]
            | flatten
            | join.lines
    ]
]

define :StandaloneResult [
    init: constructor [tests :block]

    string: method [][
        if empty? this\tests -> return ""

        tests: this\tests
            | as [:TestResult]
            | as [:string]
            | map => indent

        @["Standalone Tests:" tests]
            | flatten
            | join.lines
    ]
]

define :SpecResult [
    init: constructor [description :string tests :block]

    string: method [][
        description: @["Describe:" this\description]
            | join.words

        tests: this\tests
            | as [:TestResult]
            | as [:string]
            | map => indent

        @[description tests]
            | flatten
            | join.lines
    ]
]

define :FileResult [
    init: constructor [filename :string standalone :block specs :block]

    string: method [][
        filename: @["========" this\filename "========"]
            | join.words

        standalone: @[@[this\standalone]]
            | as [:StandaloneResult]
            | as [:string]

        specs: this\specs
            | as [:SpecResult]
            | as [:string]

        @[filename standalone specs]
            | flatten
            | join.lines
    ]

]

define :Result [
    init: constructor [files :block]

    block: method [][
        this\files 
            | map => [read.json &]
            | map => [to :FileResult &]
    ]

    string: method [][
        to :block this | join.lines
    ]
]

findResults: $[at :string][
    files: at 
        | list.recursive 
        | map => normalize

    to :Result [files]
]

